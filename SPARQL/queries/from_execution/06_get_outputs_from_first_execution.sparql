PREFIX	dc: <http://purl.org/dc/elements/1.1/>
PREFIX	dcterms: <http://purl.org/dc/terms/>
PREFIX	s: <http://schema.org/>
PREFIX	bs: <https://bioschemas.org/>
PREFIX	bswfprofile: <https://bioschemas.org/profiles/ComputationalWorkflow/>
PREFIX	bsworkflow: <https://bioschemas.org/ComputationalWorkflow#>
PREFIX	rocrate: <https://w3id.org/ro/crate/>
PREFIX	wfcrate: <https://w3id.org/workflowhub/workflow-ro-crate/>
PREFIX	wfhprofile: <https://about.workflowhub.eu/Workflow-RO-Crate/>
PREFIX	wrprocess: <https://w3id.org/ro/wfrun/process/>
PREFIX	wrwf: <https://w3id.org/ro/wfrun/workflow/>
PREFIX	wrterm: <https://w3id.org/ro/terms/workflow-run#>
PREFIX	wikidata: <https://www.wikidata.org/wiki/>
PREFIX	wfexs: <shttp:///>

SELECT  ?output ?name ?alternate_name ?outputfp ?default_value ?additional_type ?fileuri ?value ?component ?leaf_type
WHERE   {
    # This first part is just matching
    # Workflow RO-Crates and its main entity
    ?rocratejson
        a s:CreativeWork ;
        dcterms:conformsTo ?rocrateprofile ;
        s:about ?rootdataset .
    ?rootdataset a s:Dataset .
    FILTER (
        STRSTARTS(str(?rocrateprofile), str(rocrate:))
    ) .
    ?rocratejson dcterms:conformsTo ?wfcrateprofile .
    FILTER (
        ?wfcrateprofile = wfhprofile: || STRSTARTS(str(?wfcrateprofile), str(wfcrate:))
    ) .
    ?rootdataset
        s:mainEntity ?main_entity .
    ?main_entity
        a bs:ComputationalWorkflow ;
        dcterms:conformsTo ?bsworkflowprofile .
    FILTER (
        STRSTARTS(str(?bsworkflowprofile), str(bswfprofile:))
    ) .

    # We are taking here only the first execution
    {
        SELECT  ?execution
        WHERE   {
            ?rootdataset s:mentions ?execution .
            ?execution
                a s:CreateAction ;
                s:instrument ?main_entity .
        }
        LIMIT 1
    }

    ?execution s:result ?output .
    {
        # A file, which is a schema.org MediaObject
        BIND ( "File" AS ?additional_type )
        ?output
            a s:MediaObject ;
            s:exampleOfWork ?outputfp .
        ?outputfp
            a bs:FormalParameter ;
            s:name ?name ;
            s:additionalType ?additional_type .
        OPTIONAL {
            ?output
                s:contentUrl ?fileuri .
        }
    } UNION {
        # A directory, which is a schema.org Dataset
        BIND ( "Dataset" AS ?additional_type )
        ?output
            a s:Dataset ;
            s:exampleOfWork ?outputfp .
        ?outputfp
            a bs:FormalParameter ;
            s:name ?name ;
            s:additionalType ?additional_type .
        FILTER EXISTS { 
            # subquery to determine it is not an empty Dataset
            SELECT ?dircomp
            WHERE { 
                ?output
                    s:hasPart+ ?dircomp .
                ?dircomp
                    a s:MediaObject .
            }
        }
        OPTIONAL {
            ?output
                s:contentUrl ?fileuri .
        }
    } UNION {
        # A single property value, which can be either Integer, Text, Boolean or Float
        VALUES (?additional_type) { ( "Integer" ) ( "Text" ) ( "Boolean" ) ( "Float" ) }
        ?output
            a s:PropertyValue ;
            s:exampleOfWork ?outputfp ;
            s:value ?value .
        ?outputfp
            a bs:FormalParameter ;
            s:name ?name ;
            s:additionalType ?additional_type .
    } UNION {
        # A combination of files or directories or property values
        BIND ( "Collection" AS ?additional_type )
        VALUES ( ?leaf_type ) { ( s:Integer ) ( s:Text ) ( s:Boolean ) ( s:Float ) ( s:MediaObject ) ( s:Dataset ) }
        ?output
            a s:Collection ;
            s:exampleOfWork ?outputfp ;
            s:hasPart+ ?component .
        ?outputfp
            a bs:FormalParameter ;
            s:name ?name ;
            s:additionalType ?additional_type .
        ?component
            a ?leaf_type .
        OPTIONAL {
            ?component s:contentUrl ?fileuri .
        }
        OPTIONAL {
            ?component s:value ?value .
        }
    }
    OPTIONAL {
        ?outputfp
            s:defaultValue ?default_value .
    }
    OPTIONAL {
        ?output
            s:alternateName ?alternate_name .
    }
}
