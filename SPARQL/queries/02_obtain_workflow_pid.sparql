PREFIX	dc: <http://purl.org/dc/elements/1.1/>
PREFIX	dcterms: <http://purl.org/dc/terms/>
PREFIX	s: <http://schema.org/>
PREFIX	bs: <https://bioschemas.org/>
PREFIX	bswfprofile: <https://bioschemas.org/profiles/ComputationalWorkflow/>
PREFIX	bsworkflow: <https://bioschemas.org/ComputationalWorkflow#>
PREFIX	rocrate: <https://w3id.org/ro/crate/>
PREFIX	wfcrate: <https://w3id.org/workflowhub/workflow-ro-crate/>
PREFIX	wfhprofile: <https://about.workflowhub.eu/Workflow-RO-Crate/>
PREFIX	wrprocess: <https://w3id.org/ro/wfrun/process/>
PREFIX	wrwf: <https://w3id.org/ro/wfrun/workflow/>
PREFIX	wrterm: <https://w3id.org/ro/terms/workflow-run#>
PREFIX	wikidata: <https://www.wikidata.org/wiki/>
PREFIX	wfexs: <shttp:///>

SELECT  ?identifier ?workflow_repository ?workflow_version ?workflow_url ?workflow_alternate_name ?programminglanguage_identifier ?programminglanguage_url ?programminglanguage_version
WHERE   {
    # This first part is just matching
    # Workflow RO-Crates and its main entity
    ?rocratejson
        a s:CreativeWork ;
        dcterms:conformsTo ?rocrateprofile ;
        s:about ?rootdataset .
    ?rootdataset a s:Dataset .
    FILTER (
        STRSTARTS(str(?rocrateprofile), str(rocrate:))
    ) .
    ?rocratejson dcterms:conformsTo ?wfcrateprofile .
    FILTER (
        ?wfcrateprofile = wfhprofile: || STRSTARTS(str(?wfcrateprofile), str(wfcrate:))
    ) .
    ?rootdataset
        s:mainEntity ?mainentity .
    ?mainentity
        a bs:ComputationalWorkflow ;
        dcterms:conformsTo ?bsworkflowprofile .
    FILTER (
        STRSTARTS(str(?bsworkflowprofile), str(bswfprofile:))
    ) .

    # This part extracts the different details
    ?mainentity s:programmingLanguage ?programminglanguage .
    ?programminglanguage
        a s:ComputerLanguage ;
        s:url ?programminglanguage_url .
    OPTIONAL {
        ?programminglanguage
            s:version ?programminglanguage_version .
    }
    OPTIONAL {
        ?programminglanguage
            s:identifier ?programminglanguage_identifier .
    }
    {
        {
            # The workflow is the original one
            FILTER NOT EXISTS {
                ?mainentity s:isBasedOn ?origmainentity .
                ?origmainentity
                    a bs:ComputationalWorkflow ;
                    dcterms:conformsTo ?bsworkflowprofile .
                FILTER (
                    STRSTARTS(str(?bsworkflowprofile), str(bswfprofile:))
                ) .
            }
            OPTIONAL {
                ?mainentity s:codeRepository ?workflow_repository .
            }
            OPTIONAL {
                ?mainentity s:version ?workflow_version .
            }
            OPTIONAL {
                ?mainentity s:url ?workflow_url .
            }
            OPTIONAL {
                ?mainentity s:identifier ?identifier .
            }
            OPTIONAL {
                ?mainentity s:alternateName ?workflow_alternate_name .
            }
        } UNION {
            # The workflow is a derivation of an original one
            ?mainentity s:isBasedOn ?origmainentity .
            ?origmainentity
                a bs:ComputationalWorkflow ;
                dcterms:conformsTo ?bsworkflowprofile .
            OPTIONAL {
                ?origmainentity s:codeRepository ?workflow_repository .
            }
            OPTIONAL {
                ?origmainentity s:version ?workflow_version .
            }
            OPTIONAL {
                ?origmainentity s:url ?workflow_url .
            }
            FILTER (
                STRSTARTS(str(?bsworkflowprofile), str(bswfprofile:))
            ) .
            OPTIONAL {
                ?origmainentity s:identifier ?identifier .
            }
            OPTIONAL {
                ?origmainentity s:alternateName ?workflow_alternate_name .
            }
        }
    }
}
